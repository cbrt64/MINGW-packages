# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=bzip2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.6
pkgrel=7
pkgdesc="A high-quality data compression program (mingw-w64)"
arch=('any')
url="http://www.bzip.org"
license=("custom")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'staticlibs')
source=("https://sources.archlinux.org/other/packages/bzip2/bzip2-${pkgver}.tar.gz"
        "001-bzip2-1.0.4-bzip2recover.patch"
        "002-bzip2-1.0.5-slash.patch"
        "003-bzgrep-debian-1.0.5-6.all.patch"
        "004-bzip2-cygming-1.0.6.src.all.patch"
        "005-bzip2-buildsystem.all.patch"
        "006-bzip2-1.0.6-progress.all.patch"
        "007-bzip2-1.0.6-fix-heap-use-after-free-bzip2recover.patch")
sha256sums=('a2848f34fcd5d6cf47def00461fcb528a0484d8edef8208d6d2e2909dc61d9cd'
            '0585fb92a4b409404147a3f940ed2ca03b3eaed1ec4fb68ae6ad74db668bea83'
            '672216b20cf29438ffe43ebf38b9a648d9a0ac6fdc6be55bb4181d57ed5462be'
            'fe9212dcd0400d34269877b8de44b18767b9570048b85ed2a8d3c04e03940e9a'
            '7a4f735e5600580a72bd538272b650262e4bdc38699c440f26286dc347fd9e93'
            'e519a50a4adaf05b18650d3e9d644badc66e80ca86063681ffe9a2c2226319d0'
            'f93e6b50082a8e880ee8436c7ec6a65a8f01e9282436af77f95bb259b1c7f7f7'
            '7b96cf49248ff3eb24f9e552861b91848f45526a2ce07219bef925e663afe313')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
# remove files that are created by patches.
  rm -rf README.CYGMING | true
  rm -rf aclocal.m4 | true
  rm -rf configure.ac | true
  rm -rf libbz2.def.in | true
  rm -rf bzip2.pc.in | true
  rm -rf Makefile.in | true

  patch -p1 -i "${srcdir}"/001-bzip2-1.0.4-bzip2recover.patch
  patch -p1 -i "${srcdir}"/002-bzip2-1.0.5-slash.patch
  patch -p1 -i "${srcdir}"/003-bzgrep-debian-1.0.5-6.all.patch
  patch -p1 -i "${srcdir}"/004-bzip2-cygming-1.0.6.src.all.patch
  patch -p1 -i "${srcdir}"/005-bzip2-buildsystem.all.patch
  patch -p1 -i "${srcdir}"/006-bzip2-1.0.6-progress.all.patch
  patch -p1 -i "${srcdir}"/007-bzip2-1.0.6-fix-heap-use-after-free-bzip2recover.patch
  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared

  make all-dll-shared
}

check() {
  cd "${srcdir}/build-${CARCH}"
  make -k check
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
  # rm "${pkgdir}${MINGW_PREFIX}/bin/bz"{diff,grep,more}
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
